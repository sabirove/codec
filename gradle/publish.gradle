/*
 * Copyright 2019 Sabirov Evgenii (sabirov.e@gmail.com)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'maven-publish'
apply plugin: 'signing'

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'Codec'
                description = 'Convenient bidirectional IO on top of the java.io streams'
                url = 'https://github.com/sabirove/codec'
                developers {
                    developer {
                        id = 'sabirove'
                        name = 'Evgenii Sabirov'
                        email = 'sabirov.e@gmail.com'
                    }
                }
                licenses {
                    license {
                        name = 'Apache License 2.0'
                        url = 'https://github.com/sabirove/codec/blob/master/LICENSE'
                        distribution = 'repo'
                    }
                }
                scm {
                    url = 'https://github.com/sabirove/codec'
                    connection = 'scm:https://sabirove@github.com/sabirove/codec.git'
                    developerConnection = 'scm:git://github.com/sabirove/codec.git'
                }
            }
        }
    }
    repositories {
        maven {
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username project.properties.sonatypeUsername
                password project.properties.sonatypePassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

tasks.withType(Sign) {
    onlyIf { !version.endsWith("SNAPSHOT") }
}
